# ########################################
#
#

set debug_cshrc = "false"

set hostname = `hostname`
set prompt = "${hostname}> "
set shell = /bin/tcsh
set notify
set history = 1000
set filec
set nobeep
unset autologout
setenv NAME "Eric Gustafson"
setenv EDITOR "emacs"

alias ls "ls -F"
alias pwd 'echo $cwd'
alias . exit
alias more less
alias tb 'echo -n "\033]0;${HOST}\007"'
unalias vi

if (${debug_cshrc} == 'true') then
    set ts = `date`
    echo "1) ${ts}"
endif

if (${OSTYPE} == 'cygwin') then
    #
    # Cygwin
    #
    # clean up paths with spaces early, in case cshrc needs the fix.
    setenv PATH `~/bin/cyg_fixpath.py`
    #
    alias netstat /cygdrive/c/Windows/System32/NETSTAT.EXE
    alias emacs emacs-nox
    alias knife 'knife.bat'
    setenv DISPLAY localhost:0.0
    set path = ( /usr/bin /bin ${path} ~/bin )

    if (${debug_cshrc} == 'true') then
        set ts = `date`
        echo "2) ${ts}"
    endif
else
    #
    # Unix   (aka non-Cygwin)
    #
    set prompt = "`whoami`@`hostname -s`> "
    umask 022
    limit coredumpsize 0

    #setenv GEM_HOME /var/lib/gems/1.8

    set path = ( \
        ~/bin \
    #	${GEM_HOME}/bin \
        /usr/local/bin \
        /usr/bin \
        /bin \
        /usr/bin/X11 \
        /sbin \
        /usr/sbin \
        .)

    if (${debug_cshrc} == 'true') then
        set ts = `date`
        echo "x) ${ts}"
    endif
endif

## Setup SSH agent forwarding.
##
set kc = `which keychain`  # Gentoo 'keychain'
if (-x "${kc}") then
    # timeout (720 min = 12h)
    ${kc} --timeout 720 -q `~/bin/find-ssh-keyfiles.pl`
    source ~/.keychain/${hostname}-csh
endif

if (${debug_cshrc} == 'true') then
    set ts = `date`
    echo "3) ${ts}"
endif

## Run any LOcAL rc initialization needed.
##
if ( -e ~/.cshrc-local ) then
    source ~/.cshrc-local
endif

if (${debug_cshrc} == 'true') then
    set ts = `date`
    echo "4) ${ts}"
endif

## Fix-up paths at the very end -- be defensive
##
if (${OSTYPE} == 'cygwin') then
    setenv PATH `~/bin/cyg_fixpath.py`
endif

if (${debug_cshrc} == 'true') then
    set ts = `date`
    echo "e) ${ts}"
endif

## Local Variables: 
## mode: shell-script
## End:
